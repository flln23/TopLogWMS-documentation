
# Обработки подготовки данных

>Упаковывает данные для выгрузки в служебный документ. В качестве служебного документа может использоваться Акт передачи или Движение остатков собственника. Записывает подготовленные данные в регистр сведений ОчередьПодготовкиИсходящихПакетовАОД.

###	Экспортные переменные обработки:

	Инициализация экспортных переменных обязательна.


* <мВерсия>
	
	Тип: Строка.
	
	Содержит номер версии.

###	Программный интерфейс.

#### Формирование результата подготовки данных
	
##### Используемый метод

```bsl	
	ПолучитьРезультатПодготовкиДанных(<СтруктураПараметров>)
```

##### Параметры:

* <СтруктураПараметров> Тип: Структура:

	* <РезультатОбмена> Тип: Структура:
	
			* <Статус> Тип: Произвольный.
			
			* <Ошибки> Тип: Массив.
			
	* <ЗадачаОбмена> Тип: Структура:
	
		* <Заказ> Тип: ДокументСсылка.Заказ.
	
		* <Объект> Тип: Произвольный.
		
	* <Источник> Тип: СправочникСсылка.УчастникиАОД
	
		Узел - отправить пакета.
	
	* <Получатель> Тип: СправочникСсылка.УчастникиАОД
	
		Узел - получатель пакета.
		
	* <СинхронныйРежим> Тип: Булево.
	
		Если значение параметра - Истина, сразу после завершения процедуры подготовки данных будет запущена выгрузка.
	
	* <ПараметрОбъекта> Тип: Произвольный.
	
		Дополнительный параметр. Зависит от типа выгружаемого объекта.
		
	* <ПараметрыТипаСообщений> Тип: Структура.
	
		Ключи и значения структуры загружаются из табличной части ТипСообщения.ПараметрыОбработки.

##### Описание:

>Упаковывает данные для выгрузки в служебный документ. В качестве служебного документа может использоваться Акт передачи или Движение остатков собственника. Записывает подготовленные данные в регистр сведений ОчередьПодготовкиИсходящихПакетовАОД.
	
##### Пример:

```bsl
Функция ПолучитьРезультатПодготовкиДанных(СтруктураПараметров) Экспорт

	РезультатПодготовкиДанных = Новый Структура("Объект");
	
	РезультатОбмена = СтруктураПараметров.РезультатОбмена;
	Задача = СтруктураПараметров.ЗадачаОбмена;
	Источник = СтруктураПараметров.Источник;
	Получатель = СтруктураПараметров.Получатель;
	
	ТекстОшибки = "";
	Попытка
		Заказ = Задача.Заказ;
		ТипСообщенияОбмена = СтруктураПараметров.ТипСообщенияАОД;
		
		АктПередачиОбъект = ПолучитьДокументОбменаДанными(Заказ, ТипСообщенияОбмена);
		АктПередачиОбъект.Товары.Очистить();
		
		АктПередачиОбъект.Объект = Задача.Объект;
		
		Если ТипЗнч(Задача.Объект) = Тип("СправочникСсылка.СкладскиеОперации") Тогда 
			СкладскаяОперация = Задача.Объект;
		Иначе
			СкладскаяОперация = Задача.Объект.СкладскаяОперация;
		КонецЕсли;
		
		ВыборкаСтрокРезультата = ПолучитьВыборкуСтрокРезультата(Заказ, СкладскаяОперация);
		Пока ВыборкаСтрокРезультата.Следующий() Цикл
			СтрокаТовары = АктПередачиОбъект.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТовары, ВыборкаСтрокРезультата);
		КонецЦикла;                         
		
		АктПередачиОбъект.Записать();
		
		СтруктураОчереди = Новый Структура;
		СтруктураОчереди.Вставить("Объект", АктПередачиОбъект.Ссылка);
		СтруктураОчереди.Вставить("ТипСообщения", ТипСообщенияОбмена);
		СтруктураОчереди.Вставить("УзелОтправитель", Источник);
		СтруктураОчереди.Вставить("УзелПолучатель", Получатель);
		РегистрыСведений.ОчередьПодготовкиИсходящихПакетовАОД.ДобавитьВОчередь(СтруктураОчереди);
		
		Если ТекстОшибки = "" Тогда
			РезультатПодготовкиДанных.Объект = АктПередачиОбъект.Ссылка;
		КонецЕсли;
	Исключение
		ТекстОшибки = ОписаниеОшибки();
	КонецПопытки;
	
	Если ТекстОшибки <> "" Тогда 
		РезультатОбмена.Ошибки.Добавить(СтруктураОшибки(100,, ТекстОшибки));
		РезультатОбмена.Статус = -2;
	КонецЕсли;

	Возврат РезультатПодготовкиДанных;
	
КонецФункции
```
	

