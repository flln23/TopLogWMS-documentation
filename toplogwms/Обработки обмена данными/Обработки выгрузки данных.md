	
# Обработки выгрузки данных
	
	Заполняет текст пакета в исходящем пакете обмена данными.
	
###	Экспортные переменные обработки:

	Инициализация экспортных переменных обязательна.

* <мВерсия>
	
	Тип: Строка.
	
	Содержит номер версии.
	
* <мКартыСоответствия>
	
	Тип: Структура.
	
	Автоматически заполняется данными из табличной части ПараметрыКартСоответствия справочника ТипыСообщенийАОД.
	
* <мПараметры>
	
	Тип: Структура.
	
	Автоматически заполняется данными из табличной части ПараметрыОбработки справочника ТипыСообщенийАОД.
	
* <мИмяТипаДанныхКорневогоЭлемента>
	
	Тип: Строка.
	
	Имя корневого элемента в схеме запроса.
	
*  <мИмяТипаДанныхОсновногоЭлемента>
	
	Тип: Строка.
	
	Имя типа данных единсвтенного подчиненного элемента в корневом элементе схемы запроса.
	
* <мИмяОсновногоЭлемента>
	
	Тип: Строка.
	
	Имя единственного подчиненного элемента в корневом элементе схемы запроса.
	
###	Программный интерфейс.

#### Формирование текста пакета
	
##### Используемый метод

```bsl	
ЗаполнитьТекстИсходящегоПакета(Объект, ФорматСообщения)
```

##### Параметры:

* <Объект>

	Тип: Любая ссылка.
	
	Выгружаемых объект
	
* <ФорматСообщения> 

	Тип: Строка.
	
	Допустимые значения: "XML", "JSON". Регистр не важен.

##### Возвращаемое значение

* Тип: Структура:

	* <ТекстПакета> Тип: Строка.

	Строковое представление сформированного объекта XDTO.
	
	* <ОписаниеОшибки> Тип: Строка

##### Описание:

>С помощью методов общего модуля ОбменДанными получает структуру заполнения объекта XDTO. С помощью методов общего модуля ОбменДаннымиAPI формирует объект XDTO. С помощью метода ОбменДанными.ОбъектXDTOВСтроку получает строковое представления объекта XDTO.
	
##### Пример:

```bsl
Функция ЗаполнитьТекстИсходящегоПакета(Объект, ФорматСообщения) Экспорт
	
	ТекстПакета = "";
	ОписаниеОшибки = "";
	
	Если ТипЗнч(Объект) = Тип("СправочникСсылка.ВариантыУпаковки") Тогда 
		
		Попытка
			СтруктураПараметров = АОД_ПовторноеИспользование.ФабрикаXdtoИзXsd(ЭтотОбъект.ПолучитьМакет("СхемаПакета").ПолучитьТекст());
			Фабрика = СтруктураПараметров.Фабрика;
			ПространствоИмен = СтруктураПараметров.ПространствоИмен;
			ТипЗапрос = Фабрика.Тип(ПространствоИмен, мИмяТипаДанныхКорневогоЭлемента);
			
			СтруктураКоэффицентов = ОбменДанными.ПолучитьСтруктуруКоэффицентовПересчета(мПараметры);
			СтруктураПараметров.Вставить("СтруктураКоэффицентов", СтруктураКоэффицентов);
			getKeepingVariant = Фабрика.Создать(ТипЗапрос);    
			
			KeepingVariant = ОбменДаннымиAPI.ЗаполнитьKeepingVariant(Объект, СтруктураПараметров);  
			ЗаполнитьДополнительныеПоляОбъекта(KeepingVariant, Объект);
			getKeepingVariant.KeepingVariant = KeepingVariant;
			
			getKeepingVariant.Проверить();
			
			ИмяКорневогоЭлемента = "request";
			РезультатЗаписиОбъекта = ОбменДанными.ОбъектXDTOВСтроку(getKeepingVariant, ФорматСообщения, СтруктураПараметров, ИмяКорневогоЭлемента); 
			ТекстПакета = РезультатЗаписиОбъекта.Строка;

		Исключение
			ОписаниеОшибки = ОписаниеОшибки();
		КонецПопытки;
	Иначе
		ОписаниеОшибки = "Тип объекта не соответствует типу сообщения!";
	КонецЕсли;
	
	Результат = Новый Структура("ТекстПакета, ОписаниеОшибки", ТекстПакета, ОписаниеОшибки);
	Возврат Результат;
КонецФункции
```
### Макеты

* СхемаПакета (обязательный)
	
>XSD-схема исходящего пакета
	
* ОписанияЭлементов (обязательный)

>Табличный документ с описаниями элементов схемы пакета. В первую колонку записывается полное имя элемента схемы через путь от корневого элемента. Части имени разделяются символом "_", например, CorrectionOrder_Doc_Storeman. Во вторую колонку записывается описание элемента в произвольном виде. Также в макете можно хранить описание метода, значение в первой колонке в этом случае должно быть "ОписаниеМетода".

