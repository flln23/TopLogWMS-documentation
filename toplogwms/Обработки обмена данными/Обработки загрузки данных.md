
# Обработки загрузки данных

	Обработка готовит данные для записи в регистр сведений СоставДанныхПакетовОбменаАОД, обрабатывает пакеты пакеты обмена. Используются два типа обработок загрузки данных: обработки непосредственной загрузки (загружают/обновляют данные в базе), обработки запросов получения информации из базы (формируют текст сообщения ответа на основании параметров, предеанных в пакете обмена).

###	Экспортные переменные обработки:

* <мВерсия>
	
	Тип: Строка.
	
	Содержит номер версии.
	
* <мКартыСоответствия>
	
	Тип: Структура.
	
	Автоматически заполняется данными из табличной части ПараметрыКартСоответствия справочника ТипыСообщенийАОД
	
* <мПараметры>
	
	Тип: Структура.
	
	Автоматически заполняется данными из табличной части ПараметрыОбработки справочника ТипыСообщенийАОД
	
* <мИмяТипаДанныхКорневогоЭлемента>
	
	Тип: Строка.
	
	Имя корневого элемента в схеме запроса.
	
*  <мИмяТипаДанныхОсновногоЭлемента>
	
	Тип: Строка.
	
	Имя типа данных единсвтенного подчиненного элемента в корневом элементе схемы запроса
	
* <мИмяОсновногоЭлемента>
	
	Тип: Строка.
	
	Имя единственного подчиненного элемента в корневом элементе схемы запроса
	
###	Программный интерфейс.
	
#### Разбор состава пакета

##### Используемый метод

```bsl	
РазобратьСоставПакетаОбмена(<ТекстСообщения>, <ФорматСообщения>, <Состав>, <ТекстОшибки>)
```
	 
##### Параметры:

* <ТекстСообщения> (обязательный)

	Тип: Строка.
	
	Строковое представление загружаемого объекта XDTO.
	
* <ФорматСообщения> (обязательный)

	Тип: Строка.
	
	Допустимые значения: "XML", "JSON". Регистр не важен.

* <Состав> (обязательный, выходной параметр)

	Тип: Массив.
	
	В параметр помещается результат разбора пакета.
	
* <ТекстОшибки> (обязательный, выходной параметр)

	Тип: Строка.

	В параметр помещается текст ошибки разбора пакета.
	
##### Описание:

>Метод преобразует текст исходного пакета в массив текстов пакетов основных элементов пакета. Рекомендуется использовать внутри метода вызов ОбменДанными.РазобратьСоставПакетаОбмена(<СтруктураПараметров>)
	
##### Пример:
	
```bsl		
СтруктураФабрикиДляВходящегоПакета = АОД_ПовторноеИспользование.ФабрикаXdtoИзXsd(ЭтотОбъект.ПолучитьМакет("СхемаПакета").ПолучитьТекст());
СтруктураФабрикиДляРазобранногоПакета = АОД_ПовторноеИспользование.ФабрикаXdtoИзXsd(ЭтотОбъект.ПолучитьМакет("СхемаПакета").ПолучитьТекст());
	
СтруктураПараметров = Новый Структура;
СтруктураПараметров.Вставить("СтруктураФабрикиДляВходящегоПакета", СтруктураФабрикиДляВходящегоПакета);
СтруктураПараметров.Вставить("СтруктураФабрикиДляРазобранногоПакета", СтруктураФабрикиДляРазобранногоПакета);
СтруктураПараметров.Вставить("ТекстСообщения", ТекстСообщения);
СтруктураПараметров.Вставить("ФорматСообщения", ФорматСообщения);
СтруктураПараметров.Вставить("ИмяТипаДанныхКорневогоЭлемента", мИмяТипаДанныхКорневогоЭлемента);
СтруктураПараметров.Вставить("ИмяТипаДанныхОсновногоЭлемента", мИмяТипаДанныхОсновногоЭлемента);
СтруктураПараметров.Вставить("ИмяОсновногоЭлемента", мИмяОсновногоЭлемента);
	
МассивАтрибутовКорневогоЭлемента = Новый Массив;
СтруктураПараметров.Вставить("МассивАтрибутовКорневогоЭлемента", МассивАтрибутовКорневогоЭлемента);
	
РезультатРазбора = ОбменДанными.РазобратьСоставПакетаОбмена(СтруктураПараметров);
Состав = РезультатРазбора.Состав;
ТекстОшибки = РезультатРазбора.ТекстОшибки;
```

#### Обработка состава пакета

##### Используемый метод

```bsl	
ВыполнитьЗагрузкуОбъекта(<ТекстОбъекта>, <ФорматОбъекта>)
```
	
##### Параметры:

* <ТекстОбъекта> (обязательный)

	Тип: Строка.
	
	Строковое представление загружаемого объекта XDTO.

* <ФорматОбъекта> (обязательный)

	Тип: Строка.
	
	Допустимые значения: "XML", "JSON". Регистр не важен.
 
 
##### Возвращаемое значение

	Тип: Структура
	
	Поля структуры: СозданныйОбъект, ОписаниеОшибки, ТекстСообщенияОтвета (необязательное поле).
	
##### Описание:

>Метод либо создаеи объект в базе данных, либо формирует текст сообщения ответа. Для создания объектов и формирования текстов сообщений ответа рекомендуется использовать методы общих модулей ОбменДанными и ОбменДаннымиAPI.

##### Пример создания объекта:

```bsl
Функция ВыполнитьЗагрузкуОбъекта(ТекстОбъекта, ФорматОбъекта) Экспорт
	
	Результат = Новый Структура("СозданныйОбъект, ОписаниеОшибки");

	СтруктураФабрики = АОД_ПовторноеИспользование.ФабрикаXdtoИзXsd(ЭтотОбъект.ПолучитьМакет("СхемаПакета").ПолучитьТекст());
	РезультатВерифицикации = ОбменДанными.ВерифицироватьСообщениеНаСоответствиеМодели(СтруктураФабрики, ТекстОбъекта, ФорматОбъекта, мИмяТипаДанныхОсновногоЭлемента);
	
	Если РезультатВерифицикации.Верифицировано = Ложь Тогда
		Результат.ОписаниеОшибки = "Ошибка валидации пакета! " + РезультатВерифицикации.Ошибка;
		Возврат Результат;
	КонецЕсли;
	
	ОбъектПакета = РезультатВерифицикации.Объект;
	
	Попытка 
		РезультатЗагрузкиОбъекта = РезультатЗагрузкиОбъекта(ОбъектПакета);
		Если РезультатЗагрузкиОбъекта.Ошибки.Количество() = 0 Тогда 
			ОбъектСсылка = РезультатЗагрузкиОбъекта.Объект;
		Иначе
			ТекстОшибки = РезультатЗагрузкиОбъекта.Ошибки[0];
		КонецЕсли;
	Исключение
		ТекстОшибки = ОписаниеОшибки();
	КонецПопытки;

	Результат.СозданныйОбъект = ОбъектСсылка;
	Результат.ОписаниеОшибки = ТекстОшибки;
	Возврат Результат;

КонецФункции

Функция РезультатЗагрузкиОбъекта(ОбъектПакета)
	
	Результат = Новый Структура("Объект, Ошибки", Неопределено, Новый Массив);
	
	СтруктураЗаполнения = ОбменДаннымиAPI.ЗаполнитьСтруктуруДоговора(ОбъектПакета);
	Если СтруктураЗаполнения.Ошибка <> Неопределено Тогда
		Результат.Ошибки.Добавить("Ошибка " + СтруктураЗаполнения.Ошибка.Код + ": " + СтруктураЗаполнения.Ошибка.Описание 
		+ " " + СтруктураЗаполнения.Ошибка.Объект);
		Возврат Результат;
	КонецЕсли;
	
	Удаление = Ложь;
	Если ОбъектПакета.DeletionMark <> Неопределено Тогда
		Удаление = ОбъектПакета.DeletionMark;
	КонецЕсли;
	
	Договор = ОбменДанными.СоздатьДоговор(СтруктураЗаполнения, Удаление);
	Если Договор.Ошибка = Неопределено Тогда
		Результат.Вставить("Объект", Договор.Объект);
	Иначе
		Результат.Ошибки.Добавить("Ошибка " + Договор.Ошибка.Код + ": " + Договор.Ошибка.Описание 
		+ " " + Договор.Ошибка.Объект);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции	
```

##### Пример формирования ответа:

```bsl
Функция ВыполнитьЗагрузкуОбъекта(ТекстОбъекта, ФорматОбъекта) Экспорт
	
	Результат = Новый Структура("СозданныйОбъект, ОписаниеОшибки", Справочники.ТипыСообщенийАОД.ЗагрузкаДанныхЗапросИнформацииПоТовару);

	СтруктураФабрики = АОД_ПовторноеИспользование.ФабрикаXdtoИзXsd(ЭтотОбъект.ПолучитьМакет("СхемаПакета").ПолучитьТекст());
	РезультатВерифицикации = ОбменДанными.ВерифицироватьСообщениеНаСоответствиеМодели(СтруктураФабрики, ТекстОбъекта
	, ФорматОбъекта, мИмяТипаДанныхКорневогоЭлемента);
	
	Если РезультатВерифицикации.Верифицировано = Ложь Тогда
		Результат.ОписаниеОшибки = "Ошибка валидации пакета! ";
		Возврат Результат;
	КонецЕсли;
	
	ОбъектЗапроса = РезультатВерифицикации.Объект;
	
	Попытка 
		
		СтруктураФабрики = АОД_ПовторноеИспользование.ФабрикаXdtoИзXsd(ЭтотОбъект.ПолучитьМакет("СхемаОтвета").ПолучитьТекст());
		
		Условия = ОбменДаннымиAPI.ЗаполнитьСтруктуруУсловийЗапросаНоменклатуры(ОбъектЗапроса);
		СтруктураНоменклатуры = ОбменДанными.ПолучитьСтруктуруНоменклатуры(Условия);
		СтруктураКоэффицентов = Новый Структура(Новый ФиксированнаяСтруктура(мПараметры));
		ДанныеОтвета = ОбменДаннымиAPI.ЗаполнитьОтветПолучитьНоменклатуру(СтруктураНоменклатуры, СтруктураКоэффицентов, СтруктураФабрики);
		
		ИмяКорневогоЭлемента = "Response";
		РезультатЗаписиОбъекта = ОбменДанными.ОбъектXDTOВСтроку(ДанныеОтвета, ФорматОбъекта, СтруктураФабрики, ИмяКорневогоЭлемента);
		ТекстСообщенияОтвета = РезультатЗаписиОбъекта.Строка;
		Если ЗначениеЗаполнено(РезультатЗаписиОбъекта.ТекстОшибки) Тогда
			Результат.ОписаниеОшибки = РезультатЗаписиОбъекта.ТекстОшибки;
		КонецЕсли;
		Результат.Вставить("ТекстСообщенияОтвета", ТекстСообщенияОтвета);
		
	Исключение
		Результат.ОписаниеОшибки = ОписаниеОшибки();
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции
```
	
### Макеты

* СхемаПакета (обязательный)
	
>XSD-схема входящего пакета
	
* ОписанияЭлементов (обязательный)

>Табличный документ с описаниями элементов схемы пакета. В первую колонку записывается полное имя элемента схемы через путь от корневого элемента. Части имени разделяются символом "_", например, CorrectionOrder_Doc_Storeman. Во вторую колонку записывается описание элемента в произвольном виде. Также в макете можно хранить описание метода, значение в первой колонке в этом случае должно быть "ОписаниеМетода".

* СхемаОтвета (необязательный)

>XSD-схема объекта, который будет записан в ТекстСообщенияОтвета.

	

